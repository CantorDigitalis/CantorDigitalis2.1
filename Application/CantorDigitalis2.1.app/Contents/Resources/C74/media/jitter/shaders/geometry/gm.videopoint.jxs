<jittershader name="gm.videopoint.jxs">
	<description>Simple geometry shader test</description>
	<param name="radius" type="float" default="0.02" />
	<param name="height" type="float" default="0.5" />
	<param name="rotate" type="float" default="90" />
	<param name="push" type="float" default="0.5" />
	
	<param name="Ia" type="vec4" default="0. 0.00 0. 1.0" />
	<param name="Ii" type="vec4" default="1. 1. 1. 1.0" />
	<param name="eyePosition" type="vec3" default="0. 0. 4."/>
	<param name="Ke" type="vec4" default="0.0 0.0 0.0 0.0" />
	<param name="Ka" type="vec4" default="1.0 1.0 1.0 1.0" />
	<param name="Kd" type="vec4" default="0.2 0.2 0.2 1.0" />
	<param name="Ks" type="vec4" default="1. 1. 1. 1.0" />
	<param name="Ns" type="float" default="10." />
	
	<param name="video" type="int" default="0" />

	<param name="modelViewProjectionMatrix" type="mat4" state="MODELVIEW_PROJECTION_MATRIX" />
	<param name="projectionMatrix" type="mat4" state="PROJECTION_MATRIX" />
	<param name="modelViewMatrix" type="mat4" state="MODELVIEW_MATRIX" />
	<param name="normalMatrix"    type="mat3" state="NORMAL_MATRIX" />
	<param name="textureMatrix0"  type="mat4" state="TEXTURE0_MATRIX" />
	<param name="color" type="vec4" state="COLOR" />
	<param name="position" type="vec3" state="POSITION" />
	<param name="normal" type="vec3" state="NORMAL" />
	<param name="texcoord" type="vec2" state="TEXCOORD" />
	<param name="LightingParameters" state="LIGHT" />

	<language name="glsl" version="1.2">
		<bind param="radius" program="gp" />
		<bind param="height" program="gp" />
		<bind param="rotate" program="gp" />
		<bind param="push" program="gp" />
		
		<bind param="Ia" program="fp" />
		<bind param="Ii" program="fp" />
		<bind param="eyePosition" program="fp" />
		<bind param="Ka" program="fp" />
		<bind param="Kd" program="fp" />
		<bind param="Ks" program="fp" />
		<bind param="Ns" program="fp" />
		
		<bind param="video" program="fp" />
		<program name="vp" type="vertex">
<![CDATA[

#version 120

varying vec2 texcoord0;
varying vec2 texdim0;

void main(void)
{
	//just send the things as they are
	gl_Position = gl_Vertex;
	gl_FrontColor = gl_Color;
	
	texcoord0 = vec2(gl_TextureMatrix[0] * gl_MultiTexCoord0);
	texdim0 = vec2 (abs(gl_TextureMatrix[0][0][0]),abs(gl_TextureMatrix[0][1][1]));
}

]]>		
		</program>
		<program name="gp" vertices_out="9" input_type="points" output_type="triangle_strip" type="geometry">
<![CDATA[

#version 120
#extension GL_EXT_geometry_shader4 : enable

mat3 buildRotMat(vec3 axis, float angle)
{
	mat3 m;
	
	vec3 sq = axis*axis;
	vec3 coeff = axis.xxy*axis.yzz;	// (xy, xz, yz)
	float c = cos(angle);
	float m_c = 1.-c;

	float s = sin(angle);
	float m_s = 1.-s;

	m[0][0] = sq.x*m_c + c;
	m[0][1] = coeff.x*m_c + axis.z*s;
	m[0][2] = coeff.y*m_c - axis.y*s;
	
	m[1][0] = coeff.x*m_c - axis.z*s;
	m[1][1] = sq.y*m_c + c;
	m[1][2] = coeff.z*m_c + axis.x*s;
	
	m[2][0] = coeff.y*m_c + axis.y*s;
	m[2][1] = coeff.z*m_c - axis.x*s;
	m[2][2] = sq.z*m_c + c;

	return m;
}

vec3 makeNormal(vec4 p1, vec4 p2, vec4 p3)
{
	vec3 v1 = p2.xyz - p1.xyz;
	vec3 v2 = p3.xyz - p1.xyz;
	
	return normalize( cross(v1, v2) );
}

varying in vec2 texcoord0[1];
varying in vec2 texdim0[1];
varying out vec2 texcoord0_g;
varying out vec2 texdim0_g;
varying out vec4 color_scale;

varying out vec3 Norm;
varying out vec3 Po;

const vec4 red = vec4(1., 0., 0., 1.);
const vec4 green = vec4(0., 1., 0., 1.);
const vec4 blue = vec4(0., 0., 1., 1.);

// uniform sampler2DRect vid;

uniform float radius;
uniform float height;
uniform float rotate;
uniform float push;

void main(void)
{
	vec4 center = gl_PositionIn[0];
//	float radius = 0.02;
//	float height = 0.5;
	float back = -pow(1.-gl_FrontColorIn[0].g, 2.)*push;
	
	vec3 redAxis = vec3(cos(330*3.1415/180), sin(330*3.1415/180), 0.);
	vec3 greenAxis = vec3(cos(90*3.1415/180), sin(90*3.1415/180), 0.);
	vec3 blueAxis = vec3(cos(210*3.1415/180), sin(210*3.1415/180), 0.);
	float angle = rotate*3.1415/180.;
	
	mat3 redM = buildRotMat(redAxis, angle*gl_FrontColorIn[0].r);
	mat3 greenM = buildRotMat(greenAxis, angle*gl_FrontColorIn[0].g);
	mat3 blueM = buildRotMat(blueAxis, angle*gl_FrontColorIn[0].b);
	mat3 m = redM*greenM*blueM;

	vec4 top = vec4(0., 0., height+back, 0.);
	vec4 c1 = vec4(cos(0.), sin(0.), 0., 0.)*radius;
	vec4 c2 = vec4(cos(120*3.1415/180), sin(120*3.1415/180), 0., 0.)*radius;
	vec4 c3 = vec4(cos(240*3.1415/180), sin(240*3.1415/180), 0., 0.)*radius;
	
	c1.z = back;
	c2.z = back;
	c3.z = back;
	
	top.xyz = m*top.xyz;
	c1.xyz = m*c1.xyz;
	c2.xyz = m*c2.xyz;
	c3.xyz = m*c3.xyz;
	
	
	vec4 apex = center + top;
	vec3 apexNorm = vec3(0., 0., 1.);
	
	vec4 b1 = center + c1;
	vec4 b2 = center + c2;
	vec4 b3 = center + c3;
	
	vec3 n1a = makeNormal(b1, b2, apex);
	vec3 n1b = makeNormal(b1, apex, b3);
	
	vec3 n2a = makeNormal(b2, b3, apex);
	vec3 n2b = makeNormal(b2, apex, b1);
	
	vec3 n3a = makeNormal(b3, b1, apex);
	vec3 n3b = makeNormal(b3, apex, b2);
	
	vec3 b1Norm = (n1a+n1b)*0.5;
	vec3 b2Norm = (n2a+n2b)*0.5;
	vec3 b3Norm = (n3a+n3b)*0.5;
	

//-------------  RED  ----------------------------------
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = red;
	Norm = gl_NormalMatrix*apexNorm;
	Po = apex.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*apex;
	EmitVertex();
	
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = red;
	Norm = gl_NormalMatrix*b1Norm;
	Po = b1.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*b1;
	EmitVertex();
	
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = red;
	Norm = gl_NormalMatrix*b2Norm;
	Po = b2.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*b2;
	EmitVertex();
	
	EndPrimitive();
//-----------------------------------------------	

//-------------  Green  ----------------------------------
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = green;
	Norm = gl_NormalMatrix*apexNorm;
	Po = apex.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*apex;
	EmitVertex();
	
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = green;
	Norm = gl_NormalMatrix*b2Norm;
	Po = b2.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*b2;
	EmitVertex();
	
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = green;
	Norm = gl_NormalMatrix*b3Norm;
	Po = b3.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*b3;
	EmitVertex();
	
	EndPrimitive();
//-----------------------------------------------	

//-------------  Blue  ----------------------------------
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = blue;
	Norm = gl_NormalMatrix*apexNorm;
	Po = apex.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*apex;
	EmitVertex();
	
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = blue;
	Norm = gl_NormalMatrix*b3Norm;
	Po = b3.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*b3;
	EmitVertex();
	
	gl_FrontColor = gl_FrontColorIn[0];
	texcoord0_g = texcoord0[0];
	texdim0_g = texdim0[0];
	color_scale = blue;
	Norm = gl_NormalMatrix*b1Norm;
	Po = b1.xyz;
	gl_Position = gl_ModelViewProjectionMatrix*b1;
	EmitVertex();
	
	EndPrimitive();
//-----------------------------------------------	
}


]]>
		</program>
		<program name="fp" type="fragment">
<![CDATA[

#version 120

varying vec2 texcoord0_g;
varying vec2 texdim0_g;
uniform sampler2DRect video;

uniform vec4 Ia;
uniform vec4 Ii;
uniform vec3 eyePosition;
uniform vec4 Ke;
uniform vec4 Ka;
uniform vec4 Kd;
uniform vec4 Ks;
uniform float Ns;

varying vec4 color_scale;
varying vec3 Po;
varying vec3 Norm;


void main (void)
{
	vec3 Nn = normalize(Norm);
	vec3 Vn = normalize(Po);
	
	//ambient contribution
	vec4 ambient = Ia*Ka;
	
	vec3 lightPosition = vec3(gl_LightSource[0].position);
	
	//diffuse contribution
	vec3 L = normalize(lightPosition - Po);
	vec4 diffuse = Kd*Ii*max(dot(Nn, L), 0.);
	
	//calculate specular contribution
	vec3 V = normalize(eyePosition - Po);
	vec3 H = normalize(L + V); //average of lighting and view vector)  not true reflection vector
	vec4 specular = Ks*Ii * pow(max(dot(Nn,H), 0.), Ns);
	
	gl_FragColor = Ke + ambient + diffuse + specular;
	gl_FragColor *= texture2DRect(video, texcoord0_g)*color_scale;
}


]]>		
		</program>		
	</language>

<language name="glsl" version="1.5">
	<bind param="radius" program="gp" />
	<bind param="height" program="gp" />
	<bind param="rotate" program="gp" />
	<bind param="push" program="gp" />
	
	<bind param="Ia" program="fp" />
	<bind param="Ii" program="fp" />
	<bind param="eyePosition" program="fp" />
	<bind param="Ka" program="fp" />
	<bind param="Kd" program="fp" />
	<bind param="Ks" program="fp" />
	<bind param="Ns" program="fp" />
	
	<bind param="video" program="fp" />
	<bind param="video" program="vp" />

	<bind param="position" program="vp" />
	<bind param="color" program="vp" />	
	<bind param="normal" program="vp" />
	<bind param="texcoord" program="vp" />
	<bind param="modelViewMatrix" program="vp" />
	<bind param="modelViewProjectionMatrix" program="gp" />
	<bind param="projectionMatrix" program="gp" />
	<bind param="textureMatrix0" program="vp" />
	<bind param="normalMatrix" program="gp" />
	<bind param="LightingParameters" program="fp" />

<program name="vp" type="vertex">
<![CDATA[
#version 330

uniform mat4 textureMatrix0;
uniform sampler2DRect video;

in vec3 position;
in vec4 color;
in vec2 texcoord;

out jit_PerVertex {
	vec2 texcoord0;
	vec2 texdim0;
	vec4 color;
} jit_out;

void main(void)
{
	//just send the things as they are
	gl_Position = vec4(position, 1.0);
	jit_out.color = color;
	
	jit_out.texcoord0 = vec2(textureMatrix0 * vec4(texcoord, 0., 1.));
	jit_out.texdim0 = vec2(textureSize(video)); //vec2 (abs(textureMatrix0[0][0][0]),abs(textureMatrix0[0][1][1]));
}

]]>		
		</program>
		<program name="gp" vertices_out="9" input_type="points" output_type="triangle_strip" type="geometry">
<![CDATA[
	
#version 330

layout (points) in;
layout (triangle_strip, max_vertices=9) out;

uniform mat3 normalMatrix;
uniform mat4 modelViewProjectionMatrix;

uniform float radius;
uniform float height;
uniform float rotate;
uniform float push;

in jit_PerVertex {
	vec2 texcoord0;
	vec2 texdim0;
	vec4 color;
} jit_in[];

out jit_PerVertex {
	vec2 texcoord0_g;
	vec2 texdim0_g;
	vec4 color;
	vec4 color_scale;
	vec3 Norm;
	vec3 Po;
};

mat3 buildRotMat(vec3 axis, float angle)
{
	mat3 m;
	
	vec3 sq = axis*axis;
	vec3 coeff = axis.xxy*axis.yzz;	// (xy, xz, yz)
	float c = cos(angle);
	float m_c = 1.-c;

	float s = sin(angle);
	float m_s = 1.-s;

	m[0][0] = sq.x*m_c + c;
	m[0][1] = coeff.x*m_c + axis.z*s;
	m[0][2] = coeff.y*m_c - axis.y*s;
	
	m[1][0] = coeff.x*m_c - axis.z*s;
	m[1][1] = sq.y*m_c + c;
	m[1][2] = coeff.z*m_c + axis.x*s;
	
	m[2][0] = coeff.y*m_c + axis.y*s;
	m[2][1] = coeff.z*m_c - axis.x*s;
	m[2][2] = sq.z*m_c + c;

	return m;
}

vec3 makeNormal(vec4 p1, vec4 p2, vec4 p3)
{
	vec3 v1 = p2.xyz - p1.xyz;
	vec3 v2 = p3.xyz - p1.xyz;
	
	return normalize( cross(v1, v2) );
}

const vec4 red = vec4(1., 0., 0., 1.);
const vec4 green = vec4(0., 1., 0., 1.);
const vec4 blue = vec4(0., 0., 1., 1.);

// uniform sampler2DRect vid;

void main(void)
{
	vec4 center = gl_in[0].gl_Position;
//	float radius = 0.02;
//	float height = 0.5;
	float back = -pow(1.-jit_in[0].color.g, 2.)*push;
	
	vec3 redAxis = vec3(cos(330*3.1415/180), sin(330*3.1415/180), 0.);
	vec3 greenAxis = vec3(cos(90*3.1415/180), sin(90*3.1415/180), 0.);
	vec3 blueAxis = vec3(cos(210*3.1415/180), sin(210*3.1415/180), 0.);
	float angle = rotate*3.1415/180.;
	
	mat3 redM = buildRotMat(redAxis, angle*jit_in[0].color.r);
	mat3 greenM = buildRotMat(greenAxis, angle*jit_in[0].color.g);
	mat3 blueM = buildRotMat(blueAxis, angle*jit_in[0].color.b);
	mat3 m = redM*greenM*blueM;

	vec4 top = vec4(0., 0., height+back, 0.);
	vec4 c1 = vec4(cos(0.), sin(0.), 0., 0.)*radius;
	vec4 c2 = vec4(cos(120*3.1415/180), sin(120*3.1415/180), 0., 0.)*radius;
	vec4 c3 = vec4(cos(240*3.1415/180), sin(240*3.1415/180), 0., 0.)*radius;
	
	c1.z = back;
	c2.z = back;
	c3.z = back;
	
	top.xyz = m*top.xyz;
	c1.xyz = m*c1.xyz;
	c2.xyz = m*c2.xyz;
	c3.xyz = m*c3.xyz;
	
	
	vec4 apex = center + top;
	vec3 apexNorm = vec3(0., 0., 1.);
	
	vec4 b1 = center + c1;
	vec4 b2 = center + c2;
	vec4 b3 = center + c3;
	
	vec3 n1a = makeNormal(b1, b2, apex);
	vec3 n1b = makeNormal(b1, apex, b3);
	
	vec3 n2a = makeNormal(b2, b3, apex);
	vec3 n2b = makeNormal(b2, apex, b1);
	
	vec3 n3a = makeNormal(b3, b1, apex);
	vec3 n3b = makeNormal(b3, apex, b2);
	
	vec3 b1Norm = (n1a+n1b)*0.5;
	vec3 b2Norm = (n2a+n2b)*0.5;
	vec3 b3Norm = (n3a+n3b)*0.5;
	

//-------------  RED  ----------------------------------
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = red;
	Norm = normalMatrix*apexNorm;
	Po = apex.xyz;
	gl_Position = modelViewProjectionMatrix*apex;
	EmitVertex();
	
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = red;
	Norm = normalMatrix*b1Norm;
	Po = b1.xyz;
	gl_Position = modelViewProjectionMatrix*b1;
	EmitVertex();
	
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = red;
	Norm = normalMatrix*b2Norm;
	Po = b2.xyz;
	gl_Position = modelViewProjectionMatrix*b2;
	EmitVertex();
	
	EndPrimitive();
//-----------------------------------------------	

//-------------  Green  ----------------------------------
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = green;
	Norm = normalMatrix*apexNorm;
	Po = apex.xyz;
	gl_Position = modelViewProjectionMatrix*apex;
	EmitVertex();
	
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = green;
	Norm = normalMatrix*b2Norm;
	Po = b2.xyz;
	gl_Position = modelViewProjectionMatrix*b2;
	EmitVertex();
	
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = green;
	Norm = normalMatrix*b3Norm;
	Po = b3.xyz;
	gl_Position = modelViewProjectionMatrix*b3;
	EmitVertex();
	
	EndPrimitive();
//-----------------------------------------------	

//-------------  Blue  ----------------------------------
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = blue;
	Norm = normalMatrix*apexNorm;
	Po = apex.xyz;
	gl_Position = modelViewProjectionMatrix*apex;
	EmitVertex();
	
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = blue;
	Norm = normalMatrix*b3Norm;
	Po = b3.xyz;
	gl_Position = modelViewProjectionMatrix*b3;
	EmitVertex();
	
	color = jit_in[0].color;
	texcoord0_g = jit_in[0].texcoord0;
	texdim0_g = jit_in[0].texdim0;
	color_scale = blue;
	Norm = normalMatrix*b1Norm;
	Po = b1.xyz;
	gl_Position = modelViewProjectionMatrix*b1;
	EmitVertex();
	
	EndPrimitive();
//-----------------------------------------------	
}


]]>
		</program>
		<program name="fp" type="fragment">
<![CDATA[

#version 330

layout (location = 0) out vec4 color;

struct LightModelParameters {
	vec4 ambient;
};

struct LightSourceParameters {
	vec4 ambient;
	vec4 diffuse;
	vec4 specular;
	vec4 position;
	vec3 spotDirection;
	float spotExponent;
	float spotCutoff;
	float spotCosCutoff;
	float constantAttenuation;
	float linearAttenuation;
	float quadraticAttenuation;
};

layout (std140) uniform LightingParameters {
	LightModelParameters lightModel;
	LightSourceParameters light[1];
};

in jit_PerVertex {
	vec2 texcoord0_g;
	vec2 texdim0_g;
	vec4 color;
	vec4 color_scale;
	vec3 Norm;
	vec3 Po;
} jit_in;

uniform sampler2DRect video;

uniform vec4 Ia;
uniform vec4 Ii;
uniform vec3 eyePosition;
uniform vec4 Ke;
uniform vec4 Ka;
uniform vec4 Kd;
uniform vec4 Ks;
uniform float Ns;

void main (void)
{
	vec3 Nn = normalize(jit_in.Norm);
	vec3 Vn = normalize(jit_in.Po);
	
	//ambient contribution
	vec4 ambient = Ia*Ka;
	
	vec3 lightPosition = vec3(light[0].position.xyz);
	
	//diffuse contribution
	vec3 L = normalize(lightPosition - jit_in.Po);
	vec4 diffuse = Kd*Ii*max(dot(Nn, L), 0.);
	
	//calculate specular contribution
	vec3 V = normalize(eyePosition - jit_in.Po);
	vec3 H = normalize(L + V); //average of lighting and view vector)  not true reflection vector
	vec4 specular = Ks*Ii * pow(max(dot(Nn,H), 0.), Ns);
	
	color = Ke + ambient + diffuse + specular;
	color *= texture(video, jit_in.texcoord0_g)*jit_in.color_scale;
}

]]>		
		</program>		
	</language>
</jittershader>